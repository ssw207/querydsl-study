buildscript {
	ext {
		queryDslVersion = "5.0.0"
	}
}

plugins {
	id 'org.springframework.boot' version '2.6.3'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id "com.ewerk.gradle.plugins.querydsl" version "1.0.10" //querydsl 추가
	id 'java'
}

group = 'study'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	//lombock
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	//querydsl
	implementation "com.querydsl:querydsl-jpa:${queryDslVersion}" //querydsl 추가 (실제 어플리케이션 작성시 필요)
	annotationProcessor "com.querydsl:querydsl-apt:${queryDslVersion}" //Q파일 생성시 사용

	//db
	runtimeOnly 'com.h2database:h2'

	//logging
	implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.5.8' // 쿼리에 파라미터 로그를 편하게 보기위해 추가

	//dev
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	//test
	testCompileOnly 'org.projectlombok:lombok'//테스트에서 lombok 사용
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine' // JUnit 5를 사용하기 위함
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

//querydsl 추가 시작 (플러그인과 연동되 q파일생성)
def querydslDir = "$buildDir/generated/querydsl" // Q파일이 해당경로에 생성됨.

querydsl {
	jpa = true
	querydslSourcesDir = querydslDir
}
sourceSets { // 인텔리제이에서 자동으로 소스폴더에 추가함
	main.java.srcDir querydslDir
}
configurations {
	querydsl.extendsFrom compileClasspath
}
compileQuerydsl {
	options.annotationProcessorPath = configurations.querydsl
}
//querydsl 추가 끝